package  com.canappi.connectors.zillow ;

connection zillow {

 	operation searchForAddress

		GET 'http://www.zillow.com/webservice/GetSearchResults.htm?zws-id=X1-ZWz1d60dgxpki3_agge7&zpid=48749425&address=+{address.street}&citystatezip=+{address.city}' address.street address.city {
		protocol HTTP ;
		resultSet 'results' ; 
	} 

}


connection googleStreetView {
	

	operation init getPicture GET 'http://maps.googleapis.com/maps/api/streetview?size=600x400&location=${lat},%20${long}&fov=90&heading=235&pitch=10&sensor=false' lat long {
		protocol HTTP ; resultSet image 'streetviews' ('streetview');
	}
	
}


mapping resultsMapping { {"address" : "address.street" }, {"citystatezip" : "address.city" }, {"lat" : "address.latitude" },{"long" : "address.longitude" } } 

	

layout property  {
	text address (20,55,280,25) {   backgroundColor clear ; Left ; placeholder 'enter address of property'; border rounded ;}
	text citystatezip (,90,280,25) {  backgroundColor clear ; Left ;  placeholder 'enter city state zip' ; border rounded ;} 
	
	button submit 'Find Coordinates' (80,150,160,30) { 
		Gradient GreenConfirm ;  
		action searchZillow invokeOperation zillow searchForAddress ;
	}

	label latLabel 'Lat' (20,200,80,25) { Right ;  }
	label longLabel 'Long' (,240,80,25) { Right ; } 
	text lat (110,200,120,25) { backgroundColor clear ; readOnly ;  border rounded ; }
	text long (,240,120,25) {  backgroundColor clear ; readOnly ; border rounded ; } 

	button getPicture 'Show street view' (80,300,160,30) { 
		Gradient GreenConfirm ;  
		action showStreetView navigate to streetView with lat, long ;
	}
	
}

layout streetLayout {
	
	image streetview (20,100,200,133) { '' ; } ;
	
}


view streetView 'Street View' {
	color white ;
	controls  {
		layout streetLayout bindings googleStreetView  ;	
	}
}

view searchView 'Search Property' uses user defaults {
	color groupTableViewBackground ;
	controls  {
		layout property bindings zillow mapping resultsMapping ;	
	}
}


main zillow {
	navigationBar ;
	start searchView ;
	menu {  searchView }
}