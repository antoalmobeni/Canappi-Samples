package com.canappi.connectors.youtube;



connection canappiTutorials { 
	operation init getmyTubes GET 'https://gdata.youtube.com/feeds/api/users/canappi/uploads' {
		protocol ATOM ; resultSet 'feed' ('entry') ; 
	} ;
}

mapping youTubeMapping {{"videoTitle":"title"},{"description":"content"},{"thumbnailYouTube":"media:group:url"},
	{"url":"media:group:url"}
}	


layout youTubeRowIPad {
	//The web control launches the YouTube video but it only works on a real device
	//The simulator will display a red box instead of the YouTube video thumbnail
	web thumbnailYouTube (5,5,240,180) { }
	label videoTitle 'title' (280,5,400,40) { font 'Helvetica-Bold' ; size 22 ; color blue ; Left ; } 
	label description 'content' (280,50,400,145) { font 'Helvetica-Oblique'; size 16 ; Left ; lines 4 ; } 
	
}


layout youTubeRow {
	//The web control launches the YouTube video but it only works on a real device
	//The simulator will display a red box instead of the YouTube video thumbnail
	web thumbnailYouTube (5,5,80,60) { }
	label videoTitle 'title' (90,3,225,25) { font 'Helvetica-Bold' ; Left ; } 
	label description 'content' (90,30,225,70) { font 'Helvetica-Oblique'; Left ; lines 3 ; } 
	
}

layout youTubeLayoutIPad stacked { 
	table tutorial {
		youTubeRowIPad[];
		rowHeight 200 ;
	}
}


layout youTubeLayout stacked { 
	table tutorial {
		youTubeRow[];
		rowHeight 130;
	}
	tablet youTubeLayoutIPad ;
}


view youTubeView 'Tutorials' {
	color groupTableViewBackground ;
	controls stacked {
    	layout youTubeLayout bindings canappiTutorials mapping youTubeMapping;
     }
}

main DataConnection {
	navigationBar ;
	start youTubeView ;
	menu { youTubeView }
}